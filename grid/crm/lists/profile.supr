"""
Lists
Manage contact lists in Customer Relation Management systems.
"""

name = "crm/lists"
version = "1.0.1"

"""
Get All Lists
Get all available contact lists
"""
usecase GetLists safe {
  input {
    """
    Page
    Pagination cursor (obtained from `nextPage`).
    """
    page string
  }

  result {
    lists [
      {
        """
        ID
        Unique ID of the contact list.
        """
        listId! string!
        """
        Name
        Name or description of the contact list.
        """
        name string
        """
        Length
        Amount of contacts in the contact list.
        """
        length number
      }
    ]
    """
    Next page
    Next page cursor.
    """
    nextPage string
  }

  error Error

  example Successful {
    input {}
    result {
      lists = [
            {
                listId = "foo-bar-1",
                name = "The first list",
                length = 42
            },
            {
                listId = "foo-bar-2",
                name = "Second list",
                length = 8
            }
        ],
        nextPage = "opaquenextpagetoken"
    }    
  }
}

model Error {
  """
  Title
  A short, human-readable summary of the problem type.
  """
  title! string

  """
  Detail
  A human-readable explanation specific to this occurrence of the problem.
  """
  detail string
}