name = "chat/get-destinations"
version = "1.0.0"

"""
Get Destinations
Retrieve paginated list of destinations, e.g. channels in slack or discord
"""
usecase GetDestinations {
    input  {

		"""
		Server
		Representation of a server. Can be guild or server, depending on the provider capability.
		"""
        server string

        """
        Types
        List of categories of destinations
        """
        types [
            enum {
                private
                public
            }
        ]

		"""
		Limit
		Maximum number of records to retrieve
		"""
        limit number
		
		"""
		Page
		Identification of page with paginated results (cursor)
		"""
		page string
	}

    result {
	    """
        Previous Page
        Cursor to the previous page for pagination backwards
        """
        previousPage string
	
        """
        Next Page
        Cursor to the next page for pagination forwards
        """
        nextPage string

        """
        Destinations
        List of retrieved destinations
        """
        destinations [Destination]

        """
        Rate Limit
        Rate limit details
        """
        rateLimit RateLimit
    }

	error Error

    example Successful {
        input {
            types = ['public'],
            limit = 2
        }

        result {
            destinations = [
                {
                    id = '1'
                    createdAt = 1546167104
                    name = 'random'
                    numOfMembers = 100
                }
                {
                    id = '2'
                    createdAt = 1546167104
                    numOfMembers = 100
                }
            ]
        }
    }

    example Failed {
        input {
            server = 'not-existing-server'
        }

        error {
            title = 'server_not_found'
        }
    }
}

model Destination {
	"""
	Id
	Unique identifier of the destination
	"""
    id! string!

	"""
	Created At
	UNIX timestamp (e.g. 1095379198.75) when the destination was created
	"""
	createdAt! number

	"""
	Name
	Name of retrieved destination
	"""
    name string

    """
    Description
    Description of the destination. Can describe purpose or use of the destination.
    """
    description string

    """
    Num Of Member
    Number of members in retrieved destination
    """
    numOfMembers number
}   

model Error {
	"""
	Title
	A short, human-readable summary of the problem type.
	"""
	title!

	"""
	A human-readable explanation specific to this occurrence of the problem.
	"""
	detail

    """
    Rate Limit
    Rate limit details
    """
    rateLimit RateLimit
}

model RateLimit {
  """
  Bucket
  Different parts of API may have different rate limits.
  Bucket identifies to which part of API the rate limits apply.
  """
  bucket string

  """
  Total Requests
  Total requests available in the time window.
  """
  totalRequests number

  """
  Remaining Requests
  Remaining requests available in the time window.
  """
  remainingRequests number

  """
  Remaining Requests available in percents
  Remaining requests available in the time window in percents.
  """
  remainingRequestsPercentage number

  """
  Reset Timestamp
  Timestamp when the rate limits will reset (in  Unix Timestamp format).
  """
  resetTimestamp number

  """
  Retry After
  Number of seconds until reset of rate limit
  """
  retryAfter number
}
