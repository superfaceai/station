"""
Delete a message
Delete a text message from channel destination
"""
name = "chat/delete-message"
version = "1.0.0"

"""
Delete Message
Deletes a message from one destination
"""
usecase DeleteMessage {
  input {
    """
    Destination
    Channel to delete the message from
    """
    destination! string!
    
    """
    Message Id
    Id of message to delete
    """
    messageId! string!
  }

  result {
    """
    Rate Limit
    Rate limit details.
    """
    rateLimit RateLimit
  }

  error Error

  example Successful {
    input {
      destination = 'C1H9RESGL',
      messageId = '1503435956.000247',
    }

    result {
      success = true,
    }
  }

  example Failed {
    input {
      destination = 'random',
      messageId = '1503435956.000247',
    }

    error {
      title = 'invalid destination ID',
    }
  }
}

model Error {
  """
  Title
  A short, human-readable summary of the problem type.
  """
  title!

  """
  Detail
  A human-readable explanation specific to this occurrence of the problem.
  """
  detail

  """
  Rate Limit
  Rate limit details.
  """
  rateLimit RateLimit
}

model RateLimit {
  """
  Bucket
  Different parts of API may have different rate limits.
  Bucket identifies to which part of API the rate limits apply.
  """
  bucket string

  """
  Total Requests
  Total requests available in the time window.
  """
  totalRequests number

  """
  Remaining Requests
  Remaining requests available in the time window.
  """
  remainingRequests number

  """
  Remaining Requests available in percents
  Remaining requests available in the time window in percents.
  """
  remainingRequestsPercentage number

  """
  Reset Timestamp
  Timestamp when the rate limits will reset (in Unix Timestamp ms format).
  """
  resetTimestamp number

  """
  Reset After
  Number of seconds until reset of rate limit
  """
  resetAfter number

  """
  Retry After
  Number of seconds until user can retry the request
  """
  retryAfter number
}
