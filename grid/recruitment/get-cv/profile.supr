"""
Get CV
Gets a candidate's CV document.
"""
name = "recruitment/get-cv"
version = "1.0.0"

"""
Get CV
Get a candidate's CV document, by candidate ID. Returns either a URL, or binary data of the CV document.
"""
usecase GetCV safe {
  input {
    """
    Contact ID
    Unique identifier of the candidate. Use the `recruitment/list-candidates` profile to get candidate IDs.
    """
    candidateId! string!
  }

  result {
    """
    Curriculum Vitae
    The CV of the candidate.
    """
    cv {
      """
      Created At
      ISO8601 timestamp of when the CV was uploaded.
      """
      createdAt string!
      """
      Document
      Binary content and file name of the CV document.
      """
      document {
        """
        File Name
        The file name of the CV document.
        """
        fileName string!
        """
        Content
        The content of the CV document.
        """
        content
      }!
      """
      Document URL
      The URL of the CV document.
      """
      documentUrl string!
    }
    """
    Rate limit
    Rate limit details.
    """
    rateLimit RateLimit
  }

  error Error

  example Success {
    input {
      candidateId = "CANDIATE_ID"
    }
    result {
      cv = {
       createdAt = "2023-01-05T10:04:58.325Z"
       document = {
        fileName = "cv.pdf"
        content = "<BinaryData>"
       }
      }
    }
  }

  example Fail {
    input {
      candidateId = "WRONG_CANDIDATE_ID"
    }
    error {
      title = "Not Found"
      detail = "Candidate ID \"WRONG_CANDIDATE_ID\" do not exist."
      code = "CandidateNotFound"
    }
  }
}

model Error {
  """
  Error Title
  A short, human-readable summary of the problem type.
  """
  title!
  """
  Error Detail
  A human-readable explanation specific to this occurrence of the problem.
  """
  detail
  """
  Code
  Error code.
  """
  code! ErrorCode!
  """
  Rate limit
  Rate limit details.
  """
  rateLimit RateLimit
}

model RateLimit {
  """
  Bucket
  Different parts of the API may have different rate limits.
  Bucket identifies which part of API the rate limits apply to.
  """
  bucket string
  """
  Total requests
  Total requests available in the time window.
  """
  totalRequests number
  """
  Remaining requests
  Remaining requests available in the time window.
  """
  remainingRequests number
  """
  Remaining requests available in percents
  Remaining requests available in the time window in percents.
  """
  remainingRequestsPercentage number
  """
  Reset timestamp
  Timestamp when the rate limits will reset (in  Unix Timestamp format). Available only for providers without rolling rate limit window.
  """
  resetTimestamp number
}

model ErrorCode enum {
  """
  Bad request
  The request was invalid or cannot be otherwise served. 
  """
  BadRequest
  """
  Candidate not found
  The candidate ID does not exist.
  """
  CandidateNotFound
  """
  CV not found
  The CV document has not been found.
  """
  CVDocumentNotFound
  """
  Wrong integration parameter
  The integration parameter value has not been accepted.
  """
  WrongIntegrationParameter
  """
  Rate limit reached
  Returned when a request cannot be served due to the user or app reached max requests quota.
  """
  RateLimitReached
  """
  Unauthenticated
  There was a problem authenticating your request. Check that you are passing valid credentials.
  """
  Unauthenticated
  """
  Unauthorized
  The request is understood, but it has been refused or access is not allowed.
  """
  Unauthorized
  """
  Unknown error
  Returned when an unexpected error occured.
  """
  UnknownError
}
