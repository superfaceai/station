"""
Update Candidate
Update existing candidate application in ATS (Applicant Tracking System).
"""
name = "recruitment/update-candidate"
version = "1.0.0"

"""
Update Candidate
Updates candidate application for company job. Specify candidate fields in the input to be updated.
"""
usecase UpdateCandidate unsafe {
  input {
    """
    Candidate ID
    Id of a candidate to be updated.
    """
    candidateId! string!

    """
    First Name
    First name of a candidate.
    """
    firstName string!

    """
    Last Name
    Last name of a candidate.
    """
    lastName string!

    """
    Email
    Email of a candidate.
    """
    email string!

    """
    Phone
    Phone number of a candidate.
    """
    phone string

    """
    Address
    Address of a candidate.
    """
    address string	

    """
    Education
    Array of education experiences.
    """
    education [Education]

    """
    Work Experience
    Array of work experiences.
    """
    workExperience [WorkExperience]

    """
    CV (Resume)
    Object containing either URL to a CV or data with CV document (BinaryData type) and fileName with the file name.
    """
    cv { url string } | { fileName string, data }

    """
    Links
    Array of links to different profiles or portfolios of a candidate.
    """
    links [Link]

    """
    Answers
    Array of answers to questions connected to a job that candidate applies to.
    """
    answers [Answer]

    """
    Stage Id
    Unique identifier of a stage, in which candidate is in.
    """
    stageId string
  }

  result {
    rateLimit RateLimit
  }

  error RecruitmentError

  example Successful {
    input {
      candidateId = 'CANDIDATE_ID'
      firstName = 'John'
      lastName = 'Doe'
    }

    result {}
  }

  example Failed {
    input {
      candidateId = 'INVALID_CANDIDATE_ID'
      firstName = 'Jean'
    }

    error {
      title = 'Candidate Not Found'
      detail = 'Candidate with ID INVALID_CANDIDATE_ID not found.'
      code = 'CandidateNotFound'
    }
  }
}

model Error {
  """
  Error Title
  A short, human-readable summary of the problem type.
  """
  title!
  """
  Error Detail
  A human-readable explanation specific to this occurrence of the problem.
  """
  detail
  """
  Code
  Error code.
  """
  code! ErrorCode!
  """
  Rate limit
  Rate limit details.
  """
  rateLimit RateLimit
}

model RateLimit {
  """
  Bucket
  Different parts of API may have different rate limits.
  Bucket identifies to which part of API the rate limits apply.
  """
  bucket string
  """
  Total requests
  Total requests available in the time window.
  """
  totalRequests number
  """
  Remaining requests
  Remaining requests available in the time window.
  """
  remainingRequests number
  """
  Remaining requests available in percents
  Remaining requests available in the time window in percents.
  """
  remainingRequestsPercentage number
  """
  Reset timestamp
  Timestamp when the rate limits will reset (in  Unix Timestamp format). Available only for providers without rolling rate limit window.
  """
  resetTimestamp number
}

model ErrorCode enum {
  """
  Candidate not found
  The candidate ID does not exist.
  """
  CandidateNotFound
  """
  Wrong integration parameter
  The integration parameter value has not been accepted.
  """
  WrongIntegrationParameter
  """
  CV file name required
  Missing cv.fileName input. CV file name is required when cv.data input field is defined.
  """
  CVFileNameRequired
  """
  CV MIME type not supported
  CV MIME type is not supported by the provider. To get supported MIME types run CreateCandidateFeatures use case.
  """
  CVMIMETypeNotSupported
  """
  Rate limit reached
  Returned when a request cannot be served due to the user or app reached max requests quota.
  """
  RateLimitReached
  """
  Unauthenticated
  There was a problem authenticating your request. Check that you are passing valid credentials.
  """
  Unauthenticated
  """
  Unauthorized
  The request is understood, but it has been refused or access is not allowed.
  """
  Unauthorized
  """
  Unknown error
  Returned when an unexpected error occured.
  """
  UnknownError
}

"""
Get Update Candidate Features
Get features of UpdateCandidate use case supported by the provider.
"""
usecase UpdateCandidateFeatures {
  result {
    """
    CV MIME Types
    List of CV MIME types accepted by the provider. MIME types are defined in RFC2045 and registered by https://www.iana.org/.
    """
    cvMIMETypes! [string!]!
    """
    CV Upload Methods
    List of supported CV upload methods.
    """
    cvUploadMethods! [
      enum {
        """
        URL
        The cv input contains url field with publicly accessible URL of a CV file.
        """
        url = 'url'
        """
        File
        The cv input contains data field with CV file and fileName field with the file name.
        """
        file = 'file'
      }!
    ]!
 }

  example Successful {
    result {
      cvMIMETypes = ['application/pdf', 'text/rtf']
      cvUploadMethods = ['url', 'file']
    }
  }
}


"""
Education
Object representing educational experience of a candidate.
"""
model Education {
  """
  Id
  Unique identifier of candidate's education experience.
  """
  id string
  
  """
  School
  Name of a school.
  """
  school string

  """
  Degree
  Degree aquired by finishing candidate's education.
  """
  degree string

  """
  Field of Study
  Name of a field studied in candidate's education.
  """
  fieldOfStudy string
  
  """
  Started At
  ISO8601 timestamp when the education started.
  """
  startedAt string

  """
  Ended At
  ISO8601 timestamp when the education ended.
  """
  endedAt string
}

"""
Work Experience
Object representing work experience of a candidate.
"""
model WorkExperience {
  """
  Id
  Unique identifier of candidate's work experience.
  """
  id string

  """
  Position
  Name of position, candidate has occupied.
  """
  position string

  """
  Summary
  Summary of candidate's work experience. 
  """
  summary string

  """
  Company
  Name of company.
  """
  company string

  """
  Industry
  Name of industry, the company worked in.
  """
  industry string

  """
  Current
  Boolean representing whether candidate still work there.
  """
  current boolean

  """
  Started At
  ISO8601 timestamp when candidate started working.
  """
  startedAt string

  """
  Ended At
  ISO8601 timestamp when candidate stoped working.
  """
  endedAt string
}

"""
Answer
Object representing a answer to specific question from a job.
"""
model Answer {
  """
  Question Id
  Unique identifier of a question for this answer.
  """
  questionId string

  """
  Type
  Enum representing type of possible answer.
  """
  type enum {
    text
    textarea,
    number
    boolean
    multiple_choice
    multiple_select
    date
    file
  }

  """
  Value
  Value of an answer, is dependent on type of an answer.
  """
  value
}

"""
Link
Object representing a link specified by candidate.
"""
model Link {
  """
  Name
  Descriptive name of a link to some URL.
  """
  name string

  """
  URL
  Actual URL destination of the link.
  """
  url string
}
