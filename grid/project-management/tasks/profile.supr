"""
Manage tasks
Create, update, list, read and delete tasks in project management systems.
"""
name = "project-management/tasks"
version = "2.0.0"

"""
Create Task
Create a new task in project management systems.
"""
usecase CreateTask unsafe {
  input {
    """
    Project IDs
    ID of the project(s) to create the task in.
    """
    projectIds [string]

    """
    Title
    Textual title of the task.
    """
    title string
    
    """
    Description
    Free-form textual description of the task.
    """
    description string
  
    """
    Parent
    ID of the parent task (if applicable).
    """
    parent string

    """
    Assignee
    ID of the user this task should be assigned to.
    """
    assignee string
  }

  result Task

  error Error

  example success {
    input {
      projectIds = ["46ebca75-68af-42a1-916d-b228c2082d4f"]
      title = "Get milk"
      description = "We ran out of milk :("
    }

    result {
      taskId = "ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
      url = "https://example.com/tasks/ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
    }
  }
}

"""
List tasks
List all tasks in a project.
"""
usecase ListTasks safe {
  input {
    """
    Project ID
    ID of the project to read tasks from.
    """
    projectId string

    """
    Page
    Pagination cursor (obtained from `nextPage`).
    """
    page string
  }

  result {
    """
    Tasks
    Tasks of a project.
    """
    tasks! [Task]
    
    """
    Next page
    Cursor to the next page of the results, if any.
    """
    nextPage string
  }

  error Error

  example success {
    input {
      projectId = "46ebca75-68af-42a1-916d-b228c2082d4f"
    }

    result {
      tasks = [{
        taskId = "ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
        title = "Get milk"
        description = "We ran out of milk :("
        url = "https://example.com/tasks/ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
      }]
    }
  }
}

"""
Read Task
Get details of a task in project management systems.
"""
usecase ReadTask safe {
  input {
    """
    Task ID
    ID of the task to read.
    """
    taskId! string!
  }

  result Task

  error Error

  example success {
    input {
      taskId = "ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
    }

    result {
      taskId = "ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
      title = "Get milk"
      description = "We ran out of milk :("
      url = "https://example.com/tasks/ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
    }
  }
}

"""
Update Task
Update a task in project management systems.
"""
usecase UpdateTask idempotent {
  input {
    """
    Task ID
    ID of the task to update.
    """
    taskId! string!

    """
    Title
    Textual title of the task.
    """
    title string
    
    """
    Description
    Free-form textual description of the task.
    """
    description string
    
    """
    Project IDs
    IDs of projects the task belongs to.
    """
    projectIds [string]

    """
    Parent ID
    ID of the parent task (if applicable).
    """
    parent string

    """
    Assignee
    ID of the user this task should be assigned to.
    """
    assignee string
  }

  result Task

  error Error

  example success {
    input {
      taskId = "ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
      title = "Get milk"
    }

    result {
      taskId = "ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
      title = "Get milk"
      description = "Old description"
      url = "https://example.com/tasks/ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
    }
  }
}

"""
Delete Task
Delete a task in project management systems.
"""
usecase DeleteTask unsafe {
  input {
    """
    Task ID
    ID of the task to delete.
    """
    taskId! string!
  }

  error Error

  example success {
    input {
      taskId = "ffa18e32-45e6-4b2b-97fa-b8509b3a8c76"
    }
  }
}

model Task {
  """
  Task ID
  ID of the task.
  """
  taskId! string!

  """
  Project IDs
  IDs of projects the task belongs to.
  """
  projectIds [string]
  
  """
  Title
  Textual title of the task.
  """
  title string
  
  """
  Description
  Free-form textual description of the task.
  """
  description string

  """
  Parent
  ID of the parent task (if applicable).
  """
  parent string

  """
  Assignee
  ID of the user this task should be assigned to.
  """
  assignee string

  """
  URL
  URL of a task
  """
  url string
}


model Error {
  """
  Error Kind
  A machine-readable error type.
  """
  kind! ErrorKind!

  """
  Error Title
  A short, human-readable summary of the problem type.
  """
  title! string!

  """
  Error Detail
  A human-readable explanation specific to this occurrence of the problem.
  """
  detail string
}

model ErrorKind enum {
  """
  Invalid Input
  Occurrs when the application sends malformed or unsupported data to the provider.
  """
  InvalidInput

  """
  Unauthenticated
  Occurrs when the application provides incorrect authentication credentials.
  """
  Unauthenticated

  """
  Not Allowed
  Occurrs if you don't have access to the specific data or capability.
  """
  NotAllowed

  """
  Rate Limit Reached
  Occurrs when the application performs the use case too often based on the provider's limits.
  """
  RateLimitReached

  """
  Unknown Error
  Occurrs when the integration comes across a previously unknown error. More details should be provided with this error.
  """
  UnknownError
}
