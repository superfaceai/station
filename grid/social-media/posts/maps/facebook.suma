// Facebook Graph API Reference: https://developers.facebook.com/docs/graph-api/

profile = "social-media/posts@1.0"
provider = "facebook"

map GetProfilePosts {
  apiVersion = call GetApiVersion()
  profileId = input.profileId
  after = undefined
  before = undefined

  // We need to pass information whether we are iterating forward (next), or backward (previous),
  // but we use only the bare cursors, hence we need to enrich them with this info.
  set if( input.page && input.page.startsWith('next:')) {
    after = input.page.replace('next:','')
  }

  set if( input.page && input.page.startsWith('previous:')) {
    before = input.page.replace('previous:','')
  }

  call GetPageAccessToken(pageId = profileId, userAccessToken = parameters.accessToken) {
    return map error if (outcome.error) outcome.error
    
    token = outcome.data.pageAccessToken
  }

  return map error if(!token) {
    title = "Unable to get page access token"
  }

  http GET "/{apiVersion}/{profileId}/feed" {

    request {
      query {
          access_token = token
          after = after
          before = before
          limit = 25
          fields = 'id,permalink_url,created_time,message,attachments,duration,height,width'
      }
    }

    response 200 "application/json" {
      posts = call foreach(post of body.data) MapPost(post = post)
      previousPage = undefined
      nextPage = undefined

      set if(body.paging && body.paging.previous && body.paging.cursors && body.paging.cursors.before) {
        previousPage = 'previous:' + body.paging.cursors.before
      }

       set if(body.paging && body.paging.next && body.paging.cursors && body.paging.cursors.after) {
        nextPage = 'next:' + body.paging.cursors.after
      }

      return map result {
        previousPage = previousPage
        nextPage = nextPage
        posts = posts
      }
    }

    response 400 "application/json" {
      return map error {
        title = "Bad request"
        detail = body.error.message
      }
    }

    response 401 "application/json" {
      return map error {
        title = "Unauthenticated"
        detail = body.error.message
      }
    }

    response 403 "application/json" {
      return map error {
        title = "Forbidden"
        detail = body.error.message
      }
    }
  }
}

operation MapPost {
  post = args.post
  attachments = []

  set if(post.attachments) {
    attachments = call foreach(attachment of post.attachments.data) MapAttachment(attachment = attachment)
  }
  

  return {
    id = post.id
    url = post.permalink_url
    createdAt = post.created_time
    text = post.message
    attachments = attachments
  }
}

operation MapAttachment {
  attachment = args.attachment

  return {
    type = attachment.type
    url = attachment.url
    title = attachment.title
    description = attachment.text
    height = attachment.height
    width = attachment.width
    duration = attachment.duration
  }
}

operation GetPageAccessToken {
  return if(!args.pageId) {}

  apiVersion = call GetApiVersion()

  http GET "/{apiVersion}/{args.pageId}" {
    request {
      query {
        fields = 'access_token',
        access_token = args.userAccessToken
      }
    }

    response 200 "application/json" {
      return {
        pageAccessToken = body.access_token
      }
    }

    response {
      error = call MapApiError(statusCode = statusCode, body = body, headers = headers)
      fail error
    }
  }
}

operation GetApiVersion {
  return 'v12.0'
}
