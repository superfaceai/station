name = "oauth/refresh-token"
version = "1.0.0"

"""
Get Access Token from Refresh Token
Issue a new access token based on previously issued refresh token for OAuth 2.0 compatible providers.
Client authentication can be provided directly as input, or, if supported, set as provider parameter.
"""
usecase GetAccessTokenFromRefreshToken unsafe {
  input {
    """
    Refresh Token
    The refresh token previously issued to the client.
    """
    refreshToken! string!

    """
    Client ID
    Client authentication; may be also accepted as provider parameter.
    """
    clientId string!

    """
    Client Secret
    Client authentication; may be also accepted as provider parameter.
    """
    clientSecret string!
  }

  result {
    accessToken! string!
    expiresIn! number!
    scopes [string]
    tokenType string
  }

  error {
    status string
    message string
  }
}
