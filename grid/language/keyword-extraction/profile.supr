"""
Keyword Extraction
Keyword extraction is the process of automatically identifying and extracting
the most important words or phrases from a text document. The extracted keywords
can then be used to help summarize the document, or to help identify topics or themes in the text.
"""
name = "language/keyword-extraction"
version = "1.0.0"

"""
Extract Keywords
Identify and extract the most important words or phrases from a text document
"""
usecase ExtractKeywords safe {
  input {
    """
    Text
    The text you wish to extract the keywords from. Note that most providers
    require the text to be less than 1000 characters
    """
    text! string!

    """
    Text Language
    Optionally specify the exact language of the text. Useful for phrases
    that might be ambigous.
    """
    languageCode string!
  }

  result {
    keywords! [Keyword!]!
  }

  error KeywordExtractionError

  example Successful {
    input {
      text = "Artificial Intelligence (AI) is making its way into everyday developer life, but the use of AI along the API lifecycle is yet to be improved"
    }

    result {
      keywords = [
        {
          text = "Artificial Intelligence",
          importance = 0.999628
        },
        {
          text = "everyday developer life",
          importance = 0.664127
        },
        {
          text = "way",
          importance = 0.632349
        },
        {
          text = "use of AI",
          importance = 0.29227
        },
        {
          text = "API lifecycle",
          importance = 0.160556
        },
      ]
    }
  }

  example UnsupportedLanguage {
    input {
      text = "Je moje auto uzamƒçeno?"
      language = "cs"
    }

    error {
      title = "Unsupported Language"
      detail = "Please see supported languages at https://docs.example.com"
    }
  }
}

model Keyword {
  """
  Extracted Keyword
  The keyword or phrase extracted from the given text.
  """
  text! string!

  """
  Importance of Keyword
  A number between 0 and 1 indicating importance of the keyword within the given text.
  """
  importance number
}

model KeywordExtractionError {
  title! string!
  detail string!
}