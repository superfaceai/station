
"""
Shipment information

This profile covers getting information about shipment state.
"""

name = "delivery-tracking/shipment-info"
version = "1.0.0"

"""
Get shipment state
"""
usecase shipmentInfo safe {
  input {
    "Shipment tracking number
    Identifier of shipment"
    trackingNumber! string

    "Carrier
    Shipment carrier identification"
    carrier string
  }

  result [{
    "Carrier
    Shipment carrier identification"
    carrier! string

    "Shipment tracking number
    Identifier of shipment"
    trackingNumber! string

    origin! Location
    destination! Location
    status ShipmentEvent
    events! [ShipmentEvent]

    "Estimated date and time of delivery"
    estimatedDeliveryDate string
  }]

  error {
    title! string
  }
}

"
Shipment status
Status of a shipment. Harmonized across different carriers.
"
model ShipmentStatus enum {
  pre_transit
  transit
  delivered
  failure
  unknown
}

model ShipmentEvent {
  timestamp! string
  statusCode! ShipmentStatus
  statusText! string
  location Location
}

model Location {
  address! Address
}

model Address {

  "Country code
   A short text string code (ISO 3166-1 alpha-2 country code) specifying the country."
  countryCode string

  "Postal code
   Text specifying the postal code for an address."
  postalCode string

  "Address locality
   Text specifying the name of the locality, for example a city."
  addressLocality string

  "Street address
   The street address expressed as free form text. The street address is printed on paper as the first lines below the name."
  streetAddress string

}
