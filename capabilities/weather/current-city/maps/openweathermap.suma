profile = "weather/current-city@1.0"
provider = "openweathermap"

map GetCurrentWeatherInCity {
  set {
    mappedUnits = null
  }
  call mapUnits(units = input.units) {
    set {
      mappedUnits = outcome.data.result
    }
  }
  http GET "/data/2.5/weather" {
    security "apikey"
    request {
      query {
        q = input.city
        units = mappedUnits
      }
    }

    response 200 "application/json" {
      map result {
        temperature = body.main.temp
        feelsLike = body.main.feels_like
        description = body.weather[0].description
      }
    }

    response 400 {
      map error {
        title = '400 - Bad Request'
        detail = body.message
      }
    }

    response 401 {
      map error {
        title = '401 - Unauthorized'
        detail = body.message
      }
    }

    response 404 {
      map error {
        title = '404 - Not Found'
        detail = body.message
      }
    }
  }
}

operation mapUnits {
  return if (args.units === 'F') {
    result = "imperial"
  }
  return if (args.units === 'K') {
    result = "standard"
  }
  return {
    result = "metric"
  }
}
