profile = "address/geocoding@3.0"
provider = "nominatim"

// Google Geocoding API documentation
//   https://developers.google.com/maps/documentation/geocoding/start

map Geocode {
  http GET "/search" {
    request {
      query {
        format = 'json'
        q = input.query
        street = input.streetAddress
        city = input.addressLocality
        county = input.addressDistrict
        state = input.addressRegion
        country = input.addressCountry
        postalcode = input.postalCode
      }
    }

    response 200 "application/json" {
      return map error if (statusCode !== 200) {
        title = "Error geocoding address"
      }

      map result {
        latitude = body[0].lat
        longitude = body[0].lon
      }
    }
  }
}

map ReverseGeocode {
  http GET "/reverse" {
    request {
      query {
        format = 'json'
        lat = input.latitude
        lon = input.longitude
      }
    }

    response 200 "application/json" {
      return map error if (statusCode !== 200) {
        title = "Error reverse geocoding coordinates"
      }

      mappedAddress = {};
      keys = Object.keys(body.address);

      // country  
      set if (keys.includes('country')) {
        mappedAddress.addressCountry = body.address.country;
      }

      // country code
      set if (!keys.includes('country') && keys.includes('country_code')) {
        mappedAddress.addressCountry = body.address.country_code;
      }

      // region
      set if (keys.includes('state')) {
        mappedAddress.addressRegion = body.address.state;
      }

      // district
      set if (keys.includes('county')) {
        mappedAddress.addressDistrict = body.address.county;
      }

      // city district
      set if (keys.includes('city_district')) {
        mappedAddress.addressCityDistrict = body.address.city_district;
      }

      // locality - city
      set if (keys.includes('city')) {
          mappedAddress.addressLocality = body.address.city;
      }
      set if (keys.includes('city') && keys.includes('borough')) {
        mappedAddress.addressLocality = body.address.city + ' ' + body.address.borough;
      }

      // locality - town
      set if (!keys.includes('city') && keys.includes('town')) {
          mappedAddress.addressLocality = body.address.town;
      }

      // locality - village
      set if (!keys.includes('town') && keys.includes('village')) {
          mappedAddress.addressLocality = body.address.village;
      }

      // street address
      set if (keys.includes('road')) {
        mappedAddress.streetAddress = body.address.road;
      }
      set if (keys.includes('house_number')) {
          mappedAddress.streetAddress = body.address.house_number  + ' ' + mappedAddress.streetAddress;
      }

      // postal code
      set if (keys.includes('postcode')) {
        mappedAddress.postalCode = body.address.postcode;
      }
        
      // formatted address
      mappedAddress.formattedAddress = body.display_name;

      map result [mappedAddress] 
    }
  }
}
