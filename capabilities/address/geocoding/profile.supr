"""
Geocoding
Code or decode an postal address to or from geographical coordinates.
"""
name = "address/geocoding"
version = "3.1.0"

"""
Geocode address

Geocode postal address into geographical coordinates (latitude and longitude)
"""
usecase Geocode {
  input {
    query
    streetAddress
    addressLocality
    addressDistrict
    addressRegion
    addressCountry
    postalCode
  }

  result {
    latitude!
    longitude!
  }
}

"""
Reverse geocode

Decodes geographical coordinates (latitude and longitude) into postal addresses
"""
usecase ReverseGeocode {
  input {
    latitude!
    longitude!
  }

  result [{
    addressCountry
    addressRegion
    addressLocality
    streetAddress
    postalCode
    formattedAddress
  }]  
}

"Query
  Free-form query string to search for. For example, '1600 Amphitheatre Parkway, Mountain View, CA' or just 'Mountain View'"
field query

"Country
  The country. For example, USA. You can also provide the two-letter ISO 3166-1 alpha-2 country code."
field addressCountry

"Region
  The region in which the district or locality is, and which is in the country. For example, California or another appropriate first-level Administrative division"
field addressRegion

"District
  The district in which locality is, and which is in the region. For example Santa Clara County."
field addressDistrict

"Locality
  The locality in which the street address is, and which is in the district. For example, Mountain View."
field addressLocality

"City district
  The city district in which the street address is, and which is in adress locality. For example, Kings County."
field addressCityDistrict

"Postal code
  The postal code. For example, 94043."
field postalCode

"Street address
  The street address. For example, 1600 Amphitheatre Pkwy."
field streetAddress

"Latitude
  The latitude of a location. For example 37.42242 (WGS 84)."
field latitude

"Longitude
  The longitude of a location. For example -122.08585 (WGS 84)."
field longitude

"Formatted address
  Address formatted as one string"
field formattedAddress
