"""
Clean Address
Normalize postal address from user input.
"""
name = "address/clean-address"
version = "1.1.0"

"""
Clean Address

This use case is useful for cleaning up addresses input by users.

It will take an address like 
'3301 South Greenfield Road, Gilbert, AZ 85297'
and converts it to
'3301 S Greenfield Rd, Gilbert, AZ 85297'.
"""
usecase CleanAddress {
  input {
    street
    city
    state
    zipcode 
  }

  result {
    street
    city
    state
    zipcode
  }

  error {
    """
    Title
    A short, human-readable summary of the problem type.
    """
    title!

    """
    Detail
    A human-readable explanation specific to this occurrence of the problem.
    """
    detail
  }

  example Successful {
    input {
      street = '3301 South Greenfield Road',
      city = 'Gilbert',
      state = 'AZ',
      zipcode = '85297'
    }

    result {
      street = '3301 S Greenfield Rd',
      city = 'Gilbert',
      state = 'AZ',
      zipcode = '85297',
    }
  }

  example Failed {
    input {
      street = '2229 Glitchy',
      city = ' ',
      state = 'RO',
      zipcode = '45'
    }

    error {
      title = 'Bad Request'
    }
  }
}

"""
Street
Street Address
"""
field street

"""
City
City or Town
"""
field city

"""
State
State or Province
"""
field state

"""
ZIP code
Zip or Postal code
"""
field zipcode
